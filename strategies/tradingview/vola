//@version=4

// Volatility Stop
//  v4.1, 2020.01.23
// This code may be reused freely, without permission.
// It was written using the PineCoders coding conventions for Pine: http://www.pinecoders.com/coding_conventions/

study("Volatility Stop", "VStop v4", true, linktoseries = true)

// Inputs
vSrc       = input(close,      "Source")
vLength    = input(20,         "Length",          minval  = 2)
vFactorATR = input(4.6,        "ATR Factor",      minval  = 0.25, step = 0.25)
vColScheme = input("Lime/Red", "Color Scheme",    options = ["Lime/Red", "Aqua/Pink"])
vStyle     = input("Line",     "Indicator style", options = ["Line", "Circles", "Diamonds", "Arrows"])
vThickness = input(2,          "Line Thickness",  options = [0, 1, 2, 3])
var vDefaultScheme = vColScheme == "Lime/Red"
var vShowArrows    =     vStyle == "Arrows"
var vShowDiamonds  =     vStyle == "Diamonds"
var vShowCircles   =     vStyle == "Circles"

// Volatility Stop Function
f_vStop(_src, _atrLength, _atrFactor) =>
    var _max     = _src
    var _min     = _src
    var _trendUp = true
    var _stop    = 0.0
    _atrM        = nz(atr(_atrLength) * _atrFactor, tr)
    _max         := max(_max, _src)
    _min         := min(_min, _src)
    _stop        := nz(_trendUp ? max(_stop, _max - _atrM) : min(_stop, _min + _atrM), _src)
    _trendUp     := _src - _stop >= 0.0
    if _trendUp != nz(_trendUp[1], true)
        _max    := _src
        _min    := _src
        _stop   := _trendUp ? _max - _atrM : _min + _atrM
    [_stop, _trendUp]

// Calculations
[vStop, vTrendUp] = f_vStop(vSrc, vLength, vFactorATR)
vTrendReversal    = vTrendUp != vTrendUp[1]

// Coloring
vColorUp   = vDefaultScheme ? #00FF00ff : #00C0FFff
vColorDown = vDefaultScheme ? #FF0000ff : #FF0080ff
vColor     = vDefaultScheme ?  vTrendUp ? #00FF00ff : #FF0000ff : vTrendUp ? #00C0FFff : #FF0080ff
vColorLine = vTrendReversal ? #00000000 : vColor

// Plotting
plotshape(vShowDiamonds or vShowCircles ? vStop : na, "Diamonds & Circles", vShowDiamonds ? shape.diamond : shape.circle, location.absolute, vColor)
plotchar(vShowArrows and vTrendUp       ? vStop : na, "Arrows Up", "⮝", location.absolute, vColorUp)
plotchar(vShowArrows and not vTrendUp   ? vStop : na, "Arrows Dn", "⮟", location.absolute, vColorDown)

plot(vThickness != 0 ? vStop : na, "V-Stop",            vColorLine, vThickness)
plot(vTrendReversal  ? vStop : na, "Beg. Circle",       vColor,     max(vThickness, 1) + 2, plot.style_circles)
plot(vTrendReversal  ? vStop : na, "Beg. Small circle", #000000ff,  max(vThickness, 1),     plot.style_circles)

// Alerts
alertcondition(vTrendReversal,               "1. V-Stop Trend Reversal",    "V-Stop Trend Reversal")
alertcondition(vTrendUp and not vTrendUp[1], "2. V-Stop Trend Up",          "V-Stop In Up Trend")
alertcondition(not vTrendUp and vTrendUp[1], "3. V-Stop Trend Down",        "V-Stop In Down Trend")
